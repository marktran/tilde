---
name: openprose-compiler
kind: service
---

# OpenProse Compiler

Compile semantic OpenProse source into repository IR that a deterministic
harness can validate and serve.

This is a pinned ProseScript compiler program. It is not a Forme-wired system:
the compiler itself owns the execution order and uses short, isolated sessions
to keep each lowering step on a narrow context budget.

### Requires

- `source_root`: source directory to compile; default `<openprose-root>/src`
  unless `prose compile` supplies a path.
- `output_dir`: build output directory; default `dist`.

### Ensures

- `manifest_next`: valid repository IR written to
  `output_dir/manifest.next.json`.
- `diagnostics`: concise compile diagnostics with enough source paths to fix
  ambiguity.

### Shape

- `self`: orchestrate the compile flow, enforce the IR contract, and write only
  a valid manifest.
- `delegates`: source discovery, responsibility lowering, gateway lowering,
  skill resolution, tool resolution, Forme lowering, IR emission, and IR
  validation.
- `prohibited`: inventing schema fields, silently guessing ambiguous timing or
  fulfillment, installing host capabilities, recursively invoking the `prose`
  CLI.

### Strategies

- Treat Markdown source as authoritative intent and IR as disposable generated
  state.
- Load only the docs needed for the current compiler session. Do not bulk-load
  the whole skill into every delegate.
- Use `ir-v0.md` as the canonical schema. When it conflicts with natural naming
  instinct, `ir-v0.md` wins.
- Infer responsibilities, concrete trigger registrations, and fulfillment only
  when the source graph makes the relationship clear.
- Do not invent connector routes, queue names, provider payloads, secrets, or
  provider subscription setup.
- Do not invent host skill or tool availability.
- Stay inside `source_root`; do not inspect sibling examples, parent
  repositories, or unrelated source trees.
- Prefer warnings over silent assumptions when timing, fulfillment, or Forme
  wiring is ambiguous.
- Write `manifest.next.json` only after validation accepts the manifest.
- After writing `manifest.next.json`, return the result. Do not run optional
  `jq`, `sed`, shell summaries, or environment-maintenance commands; the host
  CLI performs deterministic validation after the compiler program exits.

### Execution

```prose
agent source_discoverer:
  model: "fast"
  persist: false
  prompt: """
  Discover OpenProse source files under source_root.
  Load contract-markdown.md only.
  Treat source_root as a hard boundary. Do not read parent directories or
  sibling repositories while discovering source.
  Return root-relative source records with path, kind, and optional name.
  Recognize responsibility, gateway, system, service, test, pattern, and unknown.
  Ignore dist/, runs/, state/, deps/, and generated output.
  Emit diagnostics for unreadable files, unknown structures, and duplicate names.
  """
  shape:
    self: ["source discovery", "frontmatter classification"]
    prohibited: ["semantic lowering", "IR emission"]

agent responsibility_compiler:
  model: "fast"
  persist: false
  prompt: """
  Lower kind: responsibility source into responsibility records, inferred
  triggers, judge activations, optional fulfillment intent, and diagnostics.
  Load concepts/responsibility.md, concepts/reactor.md, and compiler/ir-v0.md.
  Preserve Goal, Continuity, Criteria, and Constraints in the exact IR fields:
  goal, continuity, criteria, constraints.
  Use frontmatter `id:` as the responsibility `id` in IR. Never derive
  responsibility identity from `name:`, filepath, title, or a slug; those are
  display and source-location fields only.
  Emit one judge activation per responsibility.
  Infer cron triggers from Continuity only when cadence is clear enough for a
  standard five-field cron expression. Otherwise emit a diagnostic.
  Infer fulfillment only when one system or service relationship is clearly
  strongest. Otherwise emit a diagnostic and omit fulfillment activation.
  """
  shape:
    self: ["responsibility semantics", "judge cadence", "fulfillment inference"]
    prohibited: ["provider-specific connector setup", "Forme graph emission"]

agent gateway_compiler:
  model: "fast"
  persist: false
  prompt: """
  Lower kind: gateway source into concrete trigger records.
  Load concepts/reactor.md and compiler/ir-v0.md.
  Compile Schedule sections into cron triggers.
  Compile Receives plus Emits sections into HTTP triggers when method, path,
  responsibility, and target judge activation are clear.
  Preserve provider, auth, payload, and subscription ambiguity as diagnostics.
  Do not put sourcePath, payload, metadata, emits, wakes, or activationId on
  trigger records.
  """
  shape:
    self: ["gateway lowering", "trigger registration"]
    prohibited: ["fulfillment work", "provider subscription setup"]

agent skills_resolver:
  model: "fast"
  persist: false
  prompt: """
  Resolve declared `### Skills` for every system and service in the source
  graph.
  Load contract-markdown.md (Skills) and compiler/ir-v0.md.
  For each declared skill in colon form (namespace:name), search in order:
    1. <project>/skills/
    2. ~/.claude/skills/
    3. ~/.codex/skills/
    4. ~/.agents/skills/
  A skill resolves when one of those paths contains a directory whose name
  matches the skill name in either flat (<name>) or namespaced
  (<namespace>/<name>) layout.
  Aggregate scope: a system's declared skills apply to every sub-service, and
  a service's declarations are additive — they extend, never replace, the
  inherited set.
  Never install, modify, or remove host skills.
  Return one record per declared skill with its source component path and the
  resolved location, plus an `unresolved` array of `{ skill, sourcePath,
  searchedPaths }` entries for any skill that did not resolve.
  Emit one diagnostic with severity `error` and code `skill_unresolved` for
  each unresolved entry, naming the skill and the searched paths.
  """
  shape:
    self: ["skill resolution", "host filesystem checks", "scope aggregation"]
    prohibited: ["installing skills", "modifying host state", "guessing skill locations"]

agent tools_resolver:
  model: "fast"
  persist: false
  prompt: """
  Resolve declared `### Tools` for every system, service, and responsibility in
  the source graph.
  Load contract-markdown.md (Tools) and compiler/ir-v0.md.
  Accept deterministic CLI executable declarations in the exact
  `cli:<executable-name>` form and deterministic MCP server declarations in the
  exact `mcp:<server-name>` form. Names must be non-empty and must not contain
  path separators.
  Report malformed declarations such as `gh`, `cli:`, `mcp:`, or `cli:bin/gh`
  with a diagnostic whose severity is `error` and whose message includes
  `tool_invalid`.
  Report namespaces other than `cli` and `mcp` with a diagnostic whose severity
  is `error` and whose message includes `tool_unsupported_kind`.
  For each supported CLI declaration, check host PATH for an executable with
  that name. Do not run the executable and do not perform version or auth
  checks.
  For each supported MCP declaration, check the deterministic host MCP registry
  for that server name. Do not install, contact, or introspect the MCP server.
  Aggregate scope: a system's declared tools apply to every sub-service, and a
  service's declarations are additive -- they extend, never replace, the
  inherited set. A responsibility's declarations apply to judge observation and
  fulfillment actuation for that responsibility.
  Tool declarations do not satisfy `### Requires` and do not create Forme
  dependency-graph edges.
  Never install, modify, upgrade, or remove host tools.
  Return one aggregated Forme manifest tool record per resolved system/service
  tool using `{ kind: "cli" | "mcp", name, requiredBy }`, where `requiredBy`
  names the graph nodes that need the capability.
  Return one responsibility tool list per responsibility using
  `{ responsibilityId, tools: [{ kind: "cli" | "mcp", name }] }`; use an empty
  `tools` array when the responsibility explicitly declares no required tools.
  Responsibility tool records do not have `requiredBy`.
  Return an `unresolved` array of `{ tool, sourcePath, checked }` entries for
  any executable absent from PATH or MCP server absent from the registry. Emit
  one diagnostic with severity `error` and message code `tool_unresolved` for
  each unresolved entry, naming the tool and the lookup that was checked.
  """
  shape:
    self: ["tool resolution", "PATH executable checks", "MCP registry checks", "scope aggregation"]
    prohibited: ["installing tools", "running declared tools", "guessing tool availability"]

agent forme_compiler:
  model: "fast"
  persist: false
  prompt: """
  Lower systems and services into structured Forme manifest objects.
  Load forme.md and compiler/ir-v0.md.
  Produce only the formeManifests array entries described by ir-v0.md.
  Use executionOrder entries with exactly nodeId and dependsOn fields.
  Include resolved system/service tool requirements from tools_resolution in each Forme
  manifest's tools array. Use only `{ kind: "cli" | "mcp", name, requiredBy }`
  records, and make `requiredBy` reference graph node ids in that manifest.
  Do not include responsibility-level judge tools in a Forme manifest unless a
  system or service also declares the same tool.
  Link fulfillment activations that target systems to the matching
  formeManifestId.
  Emit warnings for wiring that cannot be represented in v0.
  """
  shape:
    self: ["Forme wiring", "dependency graph", "execution order"]
    prohibited: ["responsibility semantics", "custom manifest fields"]

agent ir_emitter:
  model: "fast"
  persist: false
  prompt: """
  Assemble the final repository IR object.
  Load compiler/ir-v0.md only.
  Emit JSON matching ir-v0.md exactly: kind, version, sources,
  responsibilities, triggers, activations, formeManifests, diagnostics.
  Arrays must always be present. Omit custom fields.
  Move commentary into diagnostics. Do not emit Markdown fences.
  """
  shape:
    self: ["IR assembly", "schema discipline"]
    prohibited: ["semantic reinterpretation", "custom fields", "Markdown output"]

agent ir_validator:
  model: "fast"
  persist: false
  prompt: """
  Validate the repository IR against compiler/ir-v0.md.
  Check exact top-level fields, required fields, allowed enum values,
  root-relative paths, trigger-to-judge links, exactly one judge activation per
  responsibility, fulfillment/source/Forme links, Forme graph references,
  executionOrder dependencies, tool requiredBy references, and diagnostic
  shape.
  Treat any diagnostic with severity error as invalid for writing.
  Return valid: true only when the manifest should be written.
  Return concrete errors with JSON paths when invalid.
  """
  shape:
    self: ["schema validation", "cross-reference validation"]
    prohibited: ["rewriting source intent", "adding missing semantics"]

agent manifest_writer:
  model: "fast"
  persist: false
  prompt: """
  Write the already validated manifest JSON to output_dir/manifest.next.json.
  Create output_dir if needed.
  Do not change, pretty-print creatively, summarize, or repair the manifest.
  Report the written path and byte count.
  """
  shape:
    self: ["artifact writing"]
    prohibited: ["schema repair", "semantic changes"]

let discovered = session: source_discoverer
  prompt: "Discover the OpenProse source graph."
  context: { source_root }

let responsibility_output = session: responsibility_compiler
  prompt: "Compile responsibilities into v0 responsibility, trigger, and activation records."
  context: { source_root, discovered }

let gateway_output = session: gateway_compiler
  prompt: "Compile gateways into v0 trigger records and activation links."
  context: { source_root, discovered, responsibility_output }

let skills_resolution = session: skills_resolver
  prompt: "Resolve declared skills for every system and service."
  context: { source_root, discovered }

if skills_resolution reports unresolved skills:
  return skills_resolution

let tools_resolution = session: tools_resolver
  prompt: "Resolve declared host tools for every system, service, and responsibility."
  context: { source_root, discovered }

if tools_resolution reports invalid, unsupported, or unresolved tools:
  return tools_resolution

let forme_output = session: forme_compiler
  prompt: "Compile systems and services into v0 Forme manifests."
  context: { source_root, discovered, responsibility_output, gateway_output, tools_resolution }

let manifest = session: ir_emitter
  prompt: "Assemble the complete v0 repository IR JSON object, including resolved tools on every responsibility."
  context: { discovered, responsibility_output, gateway_output, tools_resolution, forme_output }

let validation = session: ir_validator
  prompt: "Validate the complete manifest before it is written."
  context: { manifest }

if validation reports errors:
  return validation

let write_result = session: manifest_writer
  prompt: "Write the validated manifest."
  context: { output_dir, manifest }

return write_result
```

Before forwarding to the compiler harness, the deterministic CLI preflights
the compile target source files for responsibility `id:` and required
`### Tools` sections, then resolves declared tools only within that target
(except `prose compile .`, which preserves whole-root preflight). After this
program returns, the CLI validates the written manifest. That host validation
is the final guardrail; the compiler program should still treat `ir-v0.md` as
binding before it writes.
